# Maintainer:  Michael (https://aur.archlinux.org/account/michael_wzq)
# Contributor: Chirantan Ekbote <chirantan.ekbote at gmail.com>
# Contributor: Brian Schubert <bewschubert@gmail.com>
# shellcheck disable=SC2034,SC2154

_build_doc=OFF
_build_apps=ON
_pkgname=OpenMesh
pkgname=openmesh10
pkgver=10.0.0
pkgrel=1
pkgdesc="A generic and efficient data structure for representing and manipulating polygonal meshes, version 10"
arch=('x86_64')
url="http://www.openmesh.org"
license=('BSD-3-Clause')
provides=("${pkgname}")
conflicts=("iqmol-git" "iqmol-bin")
source=("${pkgname}-${pkgver}.tar.gz::https://www.graphics.rwth-aachen.de/media/openmesh_static/Releases/10.0/OpenMesh-10.0.0.tar.gz")

b2sums=('9859341fe01c3f7caadf8b5f420069c7bd33eebbb2e825255c1de34671ba116b77b3d46a8931662a39ebae6511c459fe09c961563effe53d27fbc5c5067e0002')

if [[ "${_build_doc}" == "ON" && "${_build_apps}" == "ON" ]]; then
    makedepends=('cmake' 'qt5-base' 'graphviz' 'doxygen')
elif [[ "${_build_doc}" == "ON" ]]; then
    makedepends=('cmake' 'graphviz' 'doxygen')
elif [[ "${_build_apps}" == "ON" ]]; then
    makedepends=('cmake' 'qt5-base')
else
    makedepends=('cmake')
fi

prepare() {
    cd "${srcdir}/OpenMesh-${pkgver}" || exit 1
    if [[ "${_build_doc}" == "ON" ]]; then
        patch -Np1 -i "${srcdir}"/doc-install.patch
    fi
}

build() {
    cd "${srcdir}/OpenMesh-${pkgver}" || exit 1
    mkdir -p build && cd build || exit 1
    cmake \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_APPS=${_build_apps} \
        ..
    make

    if [[ "${_build_doc}" == "ON" ]]; then
        make doc
    fi
}

package() {
    cd "${srcdir}"/OpenMesh-${pkgver}/build || exit 1
    make DESTDIR="${pkgdir}" install

    # install licenses
    mkdir -p "${pkgdir}"/usr/share/licenses/openmesh10/
    install -D -m644 ../LICENSE "${pkgdir}"/usr/share/licenses/openmesh10/
}
