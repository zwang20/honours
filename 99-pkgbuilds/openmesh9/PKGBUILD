# Maintainer:  Michael (https://aur.archlinux.org/account/michael_wzq)
# Contributor: Chirantan Ekbote <chirantan.ekbote at gmail.com>
# Contributor: Brian Schubert <bewschubert@gmail.com>
# shellcheck disable=SC2034,SC2154

_build_doc=OFF
_build_apps=ON
_pkgname=OpenMesh
pkgname=openmesh9
pkgver=9.0
pkgrel=1
pkgdesc="A generic and efficient data structure for representing and manipulating polygonal meshes, version 9"
arch=('x86_64')
url="http://www.openmesh.org"
license=('BSD-3-Clause')
provides=("${pkgname}")
conflicts=("iqmol-git" "iqmol-bin")
source=("${pkgname}-${pkgver}.tar.gz::https://www.graphics.rwth-aachen.de/media/openmesh_static/Releases/9.0/OpenMesh-9.0.tar.gz")

b2sums=('4b7ee8f3c336746a968c441da9bc0f585f8409bdfc41332f6ae49596bcf7de3d4d1dcc68dabd8d0035fcc69aab803067adbf9dddab69b7c707e80f7924835718')

if [[ "${_build_doc}" == "ON" && "${_build_apps}" == "ON" ]]; then
    makedepends=('cmake' 'qt5-base' 'graphviz' 'doxygen')
elif [[ "${_build_doc}" == "ON" ]]; then
    makedepends=('cmake' 'graphviz' 'doxygen')
elif [[ "${_build_apps}" == "ON" ]]; then
    makedepends=('cmake' 'qt5-base')
else
    makedepends=('cmake')
fi

prepare() {
    cd "${srcdir}/OpenMesh-9.0.0" || exit 1
    if [[ "${_build_doc}" == "ON" ]]; then
        patch -Np1 -i "${srcdir}"/doc-install.patch
    fi
}

build() {
    cd "${srcdir}/OpenMesh-9.0.0" || exit 1
    mkdir -p build && cd build || exit 1
    cmake \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_APPS=${_build_apps} \
        ..
    make

    if [[ "${_build_doc}" == "ON" ]]; then
        make doc
    fi
}

package() {
    cd "${srcdir}"/OpenMesh-9.0.0/build || exit 1
    make DESTDIR="${pkgdir}" install

    # install licenses
    mkdir -p "${pkgdir}"/usr/share/licenses/openmesh9/
    install -D -m644 ../LICENSE "${pkgdir}"/usr/share/licenses/openmesh9/
}
